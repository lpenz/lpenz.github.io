
import os
import glob
import re

env = Environment(ENV = os.environ)

maintarget = 'db/packages.db'

def debdo():
    allchanges = []
    debre = re.compile('incoming/([^_]+)_([0-9.]+)-([0-9]+)_(i386|all).deb')
    for f in glob.glob('incoming/*.deb'):
        m = debre.match(f)
        if not m:
            continue
        full = m.groups()
        pack, ov, dv, arch = full
        changes = (os.path.join('incoming', '%s_%s-%s_i386.changes' % (pack, ov, dv)))
        firstletter = pack[0]
        tpath = os.path.join('pool', 'main', firstletter, pack)
        debsources = [
            os.path.join('incoming', '%s_%s-%s_%s.deb' % (pack, ov, dv, arch)),
            changes,
            os.path.join('incoming', '%s_%s-%s_i386.build' % (pack, ov, dv)),
            os.path.join('incoming', '%s_%s-%s.diff.gz' % (pack, ov, dv)),
            os.path.join('incoming', '%s_%s-%s.dsc' % (pack, ov, dv)),
            os.path.join('incoming', '%s_%s.orig.tar.gz' % (pack, ov)),
            os.path.join(tpath, '%s_%s.orig.tar.gz' % (pack, ov)),
            ]
        for s in debsources:
            env.Depends(maintarget, debsources)
        for s in [
            '%s_%s-%s_%s.deb' % full,
            '%s_%s-%s.diff.gz' % (pack, ov, dv),
            '%s_%s-%s.dsc' % (pack, ov, dv),
            ]:
            env.SideEffect(os.path.join(tpath, s), maintarget)
        for s in [
            'checksums.db',
            'contents.cache.db',
            'references.db',
            'release.caches.db',
            'version',
            ]:
            env.SideEffect(os.path.join('db', s), maintarget)
        for s in ['Release.gpg', 'Release', 'InRelease']:
            env.SideEffect(os.path.join('dists', 'lpenz', s), maintarget)
        for d in ['main', 'contrib', 'non-free']:
            for j in [
                os.path.join('source', 'Release'),
                os.path.join('source', 'Sources.gz'),
                os.path.join('binary-i386', 'Packages'),
                os.path.join('binary-i386', 'Release'),
                os.path.join('binary-i386', 'Packages.gz'),
                ]:
                env.SideEffect(os.path.join('dists', 'lpenz', d, j), maintarget)
        env.Command(os.path.join(tpath, '%s_%s.orig.tar.gz' % (pack, ov)),
            os.path.join('incoming', '%s_%s.orig.tar.gz' % (pack, ov)),
            'cp $SOURCE $TARGET')
        allchanges.append(changes)
    return allchanges


allchanges = debdo()
env.Command('index.t2t', ['lpenz.list'] + allchanges, './indexbuild $TARGET $SOURCES')
env.Command('index.html', 'index.t2t', 'txt2tags -t html -i $SOURCE -o $TARGET')
env.Command(maintarget, allchanges, './repbuild $SOURCES')

