import os
pjoin = os.path.join

if False:
    Import = None
    env = None
Import(['env'])

env.Program('hasbug.c', CPPFLAGS='-g -Wall -Werror -Wextra -W')

env.Command('r1w3_n4_results.csv',
            ['box'],
            '$SOURCE -r 1 -w 3 -n 4 --output-results $TARGET',
            chdir=False)
env.Command('r1w3_n4_results.t2t',
            ['r1w3_n4_results.csv'],
            './tools/csvt2ttable $SOURCE $TARGET')
env.Depends('r1w3_n4_results.t2t', '../../tools/csvt2ttable')
env.T2TBHTML('r1w3_n4_results.bhtml.mako', 'r1w3_n4_results.t2t')
env.MAKO('r1w3_n4_results.html', 'r1w3_n4_results.bhtml.mako',
         MAKOFLAGS='-t htmlbare')
env.HTMLSITEFILES.add(pjoin('articles/bugprobhunt', 'r1w3_n4_results.html'))

for rw in [(1, 3), (1, 2), (1, 1)]:
    b = 'r%dw%d_n4_probabilities' % rw
    env.Command(
        b + '.csv',
        ['box'],
        '$SOURCE -r %d -w %d -n 4 --output-probabilities $TARGET' % rw,
        chdir=False)
    env.Command(
        b + '.t2t',
        [b + '.csv'],
        './tools/csvt2ttable $SOURCE $TARGET')
    env.Depends(b + '.t2t', '../../tools/csvt2ttable')
    env.Depends('index.bhtml', b + '.t2t')

env.T2TBHTML('index.bhtml.mako', 'index.t2t')
env.MAKO('index.html', 'index.bhtml.mako', MAKOFLAGS='-t htmlpage')
env.HTMLSITEFILES.add('articles/bugprobhunt/index.html')


def envcmd(t, s, c):
    env.Command(t, s, c)
    env.HTMLSITEFILES.add(pjoin('articles/bugprobhunt', t))


envcmd('r1w3_dist.png', 'pdistplot.R', '$SOURCE 1 3 $TARGET')
envcmd('r1w3_dens.png', 'pdensplot.R', '$SOURCE 1 3 $TARGET')

# envcmd('trials.csv',
# [],
# 'articles/bugprobhunt/trialdoer articles/bugprobhunt/hasbug 5000 $TARGET')
# env.Depends('trials.csv',['trialdoer','hasbug'])

envcmd('trials20.png', ['trialplot.R', 'trials.csv'], '$SOURCES $TARGET 20')

envcmd('after20.png', ['afterplot.R', 'trials.csv'], '$SOURCES $TARGET 20')

envcmd('trials5000.png',
       ['trialplot.R', 'trials.csv'], '$SOURCES $TARGET 5000')

envcmd('after5000.png', ['afterplot.R', 'trials.csv'], '$SOURCES $TARGET 5000')

envcmd('tbyn.png', ['tbyn.R', 'trials.csv'], '$SOURCES $TARGET 200 300')

if False:
    envcmd('boxballs.png',
           'boxballs.pov',
           'runsilent povray ${SOURCE.file}' +
           ' -V +FN +O${TARGET.file} +Q11 +AM2 +H240 +W320 -D',
           chdir=True)

envcmd('pdens_many.png', 'pdensplot_many.R', '$SOURCE 1 3 $TARGET')
